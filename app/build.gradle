apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true

        // Enable multiDex support 分包支持
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()//room的数据库概要、记录
                             , AROUTER_MODULE_NAME: project.getName()]// ARouter 使用的配置
            }
        }

        ndk {
            abiFilters "armeabi-v7a"// "armeabi", "arm64-v8a"
        }
    }

    sourceSets {
        //数据库概要、记录存放位置
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    // signingConfigs必须放到buildTypes之前不然无法找到release和debug信息
    signingConfigs {
        debug {
            storeFile file('sign/shygrading.jks')
            storePassword 'shygrading'
            keyAlias 'grading'
            keyPassword 'shygrading'
        }
        release {
            storeFile file('sign/shygrading.jks')
            storePassword 'shygrading'
            keyAlias 'grading'
            keyPassword 'shygrading'
        }
    }

    // 以下多维度支持
    // https://developer.android.google.cn/studio/build/build-variants#flavor-dimensions
    flavorDimensions("default")
    // 以下多渠道支持
    productFlavors {

// 渠道名
//        official {
//        }
    }

    // 替换manifestPlaceholders
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    buildTypes {
        release {
            minifyEnabled false // minifyEnabled 混淆处理
            shrinkResources false// shrinkResources 去除无用资源
            signingConfig signingConfigs.release
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            zipAlignEnabled true
        }

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = "Grading_" +
                            "${releaseTime()}_" +
                            "${defaultConfig.versionName}_" +
                            // "${variant.productFlavors[0].name}" + 渠道名
                            ".apk"
                    outputFileName = fileName
                }
            }
        }
    }

    packagingOptions {
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        jumboMode true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])


    // test
    testImplementation rootProject.ext.tests
    // kotlin
    implementation rootProject.ext.kotlins

    // base lib
    implementation project(':baselib')

    def others = rootProject.ext.otherDependencies
    // room
    implementation others["room"]
    kapt others["room-compiler"]

    // navigation
    implementation others["navigation-fragment"]
    implementation others["navigation-ui"]

    //---------------------------------------------
    // ARouter compiler
    kapt others["arouter-compiler"]

    if (isLibrary.toBoolean()) {
        // module_a
        implementation project(':module_a')
    }
}